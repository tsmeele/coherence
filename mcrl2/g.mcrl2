% Coherence model Global and Local Language with testcases
% 2022.01 TSM
%
% instructions: 
%   1) modify the last source line to select a system scenario
%      e.g. include G8 to create LTS for global protocol G8, 
%           or L8a||L8p||L8q  to create LTS for projected local protocol 
%   2) create lts by running 'make lts'   (or manually: mcrl22lps g.mcrl2|lps2lts >g.lts)
%
% Model checking:
%   - coherence test: run 'make test' to test all global protocols in turn
%   - bisimulation tests: run 'make bisim' to test G8 and G9 against their local protocol counterparts
%
% NB: use option mcrl22lps -lregular2

sort RoleName = struct a|b|p|q|r; 
sort Value = struct zero|one|two|three|four|ack;
sort SyncStates = struct first|second|more;

act  send,enqueue,trackSend,send',
     receive,dequeue,updateProp,trackReceive, receive' : RoleName # RoleName # Value;
     lastRole : RoleName;  
     lastEq : Bool;
     lock,lock',unlock,unlock':RoleName # RoleName;

proc

% Participants in our protocol have a process attribute 'prop' that is updated upon a receive
% As an extension, the property can be locked by another process (the lockholder).
% NB: requesting process blocks until lock is given

  Role'(N:RoleName,prop:Value,locked:Bool,holder:RoleName) = 
     sum from:RoleName, v:Value . ((N != from) -> updateProp(from,N,v).Role'(N,v,locked,holder)) +
     sum requester:RoleName . ( (locked && (requester==holder)) -> unlock(requester,N).  
               Role'(N,prop,false,requester)) +
     sum requester:RoleName . ( (!locked) -> lock(requester,N).  
               Role'(N,prop,true,requester)) ;


% Channels are implementation of asynchronous communication, maximum queue size 5 is arbitrary

  Channel'(from,to:RoleName,data:List(Value),size:Int) =
     sum v:Value . ((size < 5) -> enqueue(from,to,v).Channel'(from,to,data <| v,succ(size)) ) +
     (size > 0)                -> dequeue(from,to,head(data)).Channel'(from,to,tail(data), pred(size) );


% Coherence' is a process that maintains the global coherence property 'last'
% In addition it acts as a probe, to report coherence status information (via actions):
%   lastRole(role)  : name of role that receives a new value for its prop
%   lastEq(boolean) : true if the data values of roles coh1 and coh2 are equal (after the receive action)
% Note that Coherence' is synchronized upon both send and receive. Further actions are blocked until report actions 
% are done. This behavior ensures that the next transition after lastRole is always a lastEq action 

  Coherence'(coh1,coh2:RoleName,coh1val,coh2val:Value,last:RoleName) =

     % case 1: coh1 role receives a new value
     sum from,to:RoleName,v:Value . 
     (to == coh1) -> (trackReceive(from,to,v).   
               lastRole(to).lastEq(v==coh2val).     
               Coherence'(coh1,coh2,v,coh2val,to) ) +

     % case 2: coh2 role receives a new value
     sum from,to:RoleName,v:Value . 
     (to == coh2) -> (trackReceive(from,to,v).
               lastRole(to).lastEq(v==coh1val).
               Coherence'(coh1,coh2,coh1val,v,to) ) +

     % case 3: some other role receives a new value
     sum from,to:RoleName,v:Value . 
     ((to != coh1) && (to != coh2)) -> (trackReceive(from,to,v).
               lastRole(to).lastEq(coh1val==coh2val).
               Coherence'(coh1,coh2,coh1val,coh2val,to) ) +

     % case send: just process and ignore
     sum from,to:RoleName,v:Value . (trackSend(from,to,v).
               Coherence'(coh1,coh2,coh1val,coh2val,last) ) ;


% Data transfer 'Cpq' is a basic construct of our global language:  Cpq --send--> Fpq --receive--> 1
% mcrl2 operators (sequential,option,parallel,recursion) are used to implement compositional constructs
% Our local language uses 'send' and 'receive' as its basic constructs. Tau is supported natively by mcrl2.
    
  C(from:RoleName,to:RoleName,v:Value) = send(from,to,v).receive(from,to,v); 



% Language Extension: We can add a CohUpd operation to the language so that related properties
% can be updated with guaranteed coherence protection
% This extension introduces the use of lock() and unlock() actions.
% NB: The acknowledge reply messages are added to make G and L a weak bisimulation.

  CohUpd(from:RoleName,toList:List(RoleName),v:Value) =
     ((#toList > 1) && !(from in toList)) ->               % at least 2 roles coherent, 'from' must not be one of them 
        CohUpd'(first,from,toList,v,[]);

%   CohUpd' executes the locking and communications done as part of the CohUpd construct
%       It implements a chain locking strategy where it keeps a lock on two role-properties
%       as it iterates through the list

  CohUpd'(syncState:SyncStates, from:RoleName,todoList:List(RoleName),v:Value,doneList:List(RoleName)) =
     (syncState == first)  -> 
           (lock(from,head(todoList)).C(from,head(todoList),v).C(head(todoList),from,ack).
            CohUpd'(second,from,tail(todoList),v,doneList<|head(todoList) )) +
     (syncState == second)  ->
          ( lock(from,head(todoList)).C(from,head(todoList),v).C(head(todoList),from,ack).
            CohUpd'(more,from,tail(todoList),v,doneList<|head(todoList)) ) +
     ((syncState == more) && (todoList != [])) ->
          ( lock(from,head(todoList)).C(from,head(todoList),v).C(head(todoList),from,ack).unlock(from,head(doneList)).
            CohUpd'(more,from,tail(todoList),v,tail(doneList)<|head(todoList) )) +
     ((syncState == more) && (todoList == []) ) -> 
          ((doneList != []) ->
                ( unlock(from,head(doneList)).
                CohUpd'(more,from,[],v,tail(doneList)) ))  ;
             

% ---------- a testset of concrete instances of coherence model protocol, feel free to add
  
% test of basic transitions as shown in figure Kripke structure  (excluding mirrorred cases)
% s0..s6 refer to states in figure Thesis report

  G0 = C(a,p,one).C(a,q,one);                         % coh(p,q) = true   (s0,s1,s3)
  G1 = C(a,p,one).C(a,q,two);                         % coh(p,q) = false  (s0,s1,s4)
  G2 = C(a,p,one).C(a,p,two);                         % coh(p,q) = false  (s0,s1,s6) 


% tests of data grid use case p = data object, q = replica

  G3 = C(a,p,one).C(a,q,one)||C(b,p,two).C(b,q,two);  % coh(p,q) = false  (race condition)
  G4 = C(a,p,one).C(a,q,one).C(b,p,two).C(b,q,two)+
       C(b,p,two).C(b,q,two).C(a,p,one).C(a,q,one);   % coh(p,q) = true   (race solved using a different protocol)

  G5 = lock(a,p).C(a,p,one).C(a,q,one).unlock(a,p) ||
       lock(b,p).C(b,p,two).C(b,q,two).unlock(b,p);   % coh(p,q) = true   (race solved using mutex) 

  G6 = CohUpd(a,[p,q],one) ||
       CohUpd(b,[p,q],two);                           % coh(p,q) = true (race solved using coherence construct) 
  L6a= lock(a,p).send(a,p,one).receive(p,a,ack).lock(a,q).send(a,q,one).receive(q,a,ack).unlock(a,p).unlock(a,q); 
  L6b= lock(b,p).send(b,p,two).receive(p,b,ack).lock(b,q).send(b,q,two).receive(q,b,ack).unlock(b,p).unlock(b,q);
  L6p= (receive(a,p,one).send(p,a,ack).receive(b,p,two).send(p,b,ack).tau)  +
       (receive(b,p,two).send(p,b,ack).receive(a,p,one).send(p,a,ack).tau) ;
  L6q= tau.((receive(a,q,one).send(q,a,ack).receive(b,q,two).send(q,b,ack))  +
       (receive(b,q,two).send(q,b,ack).receive(a,q,one).send(q,a,ack)));
  L6 = L6a || L6b || L6p || L6q;               

% test of chained coherence locking strategy coh(p,q) + coh(q,r)
%  part 1: check coh(p,q)

  G7 = lock(a,p).C(a,p,one).lock(a,q).C(a,q,one).
       lock(a,r).unlock(a,p).C(a,r,one).
       unlock(a,q).unlock(a,r) ||
       lock(b,p).C(b,p,two).lock(b,q).C(b,q,two).
       lock(b,r).unlock(b,p).C(b,r,two).
       unlock(b,q).unlock(b,r);                       % coh(p,q) = true

%  part 2: our mcf formula checks coh(p,q)
%          hence to check coh(q,r) we reuse protocol G6
%          and shuffle attribute names: p:=r, q:=p, r:=q
  G7a = lock(a,r).C(a,r,one).lock(a,p).C(a,p,one).
       lock(a,q).unlock(a,r).C(a,q,one).
       unlock(a,p).unlock(a,q) ||
       lock(b,r).C(b,r,two).lock(b,p).C(b,p,two).
       lock(b,q).unlock(b,r).C(b,q,two).
       unlock(b,p).unlock(b,q);                       % coh(p,q) = true


% bisimulation tests, we compare system(Gx) with projected system(Lxa||..||Lxr)
  G8 = (C(a,p,one)+C(a,p,two)).(C(p,q,one)+C(p,q,two));  % coh(p,q) = false  bisim = true
  L8a= (send(a,p,one)+send(a,p,two));
  L8p= (receive(a,p,one)+receive(a,p,two)).
       (send(p,q,one)+send(p,q,two)).tau;
  L8q= tau.(receive(p,q,one)+receive(p,q,two));
  L8 = L8a || L8p || L8q;


  G9 = C(a,p,one).C(b,q,one)                   ;      % coh(p,q) = true  bisim = false
  L9a= send(a,p,one).tau;
  L9b= tau.send(b,q,one);
  L9p= receive(a,p,one).tau;
  L9q= tau.receive(b,q,one);
  L9 = L9a || L9b || L9p || L9q;


% other testcases that exercise language constructs and model checking formula

  % interleaved update of non-related attribute
  G10= C(a,p,one).C(a,r,one).C(b,q,one);              % coh(p,q) = true 
  G11= C(a,p,one).C(a,r,one).C(a,q,two);              % coh(p,q) = false

  % choice constructs
  G12= C(a,p,one).C(a,r,one).
       (C(a,q,one) + C(a,p,two));                     % coh(p,q) = false
  G13= C(a,p,one).C(a,r,one).
       (C(a,q,one) + C(a,r,two));                     % coh(p,q) = true

  % parallel construct
  G14= C(a,p,one).C(a,q,one) || C(b,r,two);           % coh(p,q) = true

  % recursive construct
  G15= C(a,p,one).C(a,q,one).
       C(a,q,two).C(a,p,two).G15;                     % coh(p,q) = true


% ------------


% shorthands added to beautify init process ;)

  Role(N:RoleName) = Role'(N,zero,false,a);
  Chan(from,to:RoleName) = Channel'(from,to,[],0);
  Coherence(r1,r2:RoleName) = Coherence'(r1,r2,zero,zero,a);


init

  allow(
     { send', receive', lastRole,lastEq, lock',unlock'},
     comm( 
        {send|enqueue|trackSend -> send', receive|dequeue|updateProp|trackReceive -> receive',
         lock|lock -> lock', unlock|unlock -> unlock'},

        % these processes occur in every system:
        Role(a) || Role(b) || Role(p) || Role(q) || Role(r) || 
        Chan(a,p) || Chan(a,q) || Chan(a,r) || Chan(b,p) || Chan(b,q) || Chan(b,r) ||
        Chan(p,q) || Chan(p,r) || Chan(q,p) || Chan(q,r) || Chan(r,p) || Chan(r,q) ||
        Coherence(p,q) ||    

      % selected protocol instance:
      % (for automated testing, please ensure that below is last source line)

         G5   ) );
